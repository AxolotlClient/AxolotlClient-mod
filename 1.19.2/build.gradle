plugins {
    id 'java'
	id "org.quiltmc.loom" version "1.0.+"
}

group = maven_group
version = "${project.version}+${project.minecraft_1192}"
archivesBaseName = "AxolotlClient"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_1192}"
	mappings "org.quiltmc:quilt-mappings:${project.mappings_1192}:intermediary-v2"

	modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader}"

	modImplementation include("org.quiltmc.qsl.core:networking:${project.qsl_1192}+${project.minecraft_1192}")
	modImplementation include("org.quiltmc.qsl.core:resource_loader:${project.qsl_1192}+${project.minecraft_1192}")

	modImplementation ("io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_1192}") {
		exclude group: "com.terraformersmc"
	}
	modCompileOnlyApi("com.terraformersmc:modmenu:4.1.2")
	include "io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_1192}"

	implementation include(project(":common"))

	include( implementation('com.github.JnCrMx:discord-game-sdk4j:v0.5.5'))

	include(implementation("net.hypixel:hypixel-api-core:4.1") {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.apache.httpcomponents", module: "httpclient"
	})

	include(implementation("net.hypixel:hypixel-api-transport-apache:4.1") {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.apache.httpcomponents", module: "httpclient"
	})
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

java {
	withSourcesJar()
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/"+repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "p2rxzX0q"
	versionNumber = "${project.version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions = ["${project.minecraft_1192}"]
	loaders = ["quilt"]
	additionalFiles = [remapSourcesJar]
	dependencies {
		required.project "qsl"
	}

	// Changelog fetching: Credit LambdAurora.
	// https://github.com/LambdAurora/LambDynamicLights/blob/1ef85f486084873b5d97b8a08df72f57859a3295/build.gradle#L145
	// License: MIT
	def changelogText = file('../CHANGELOG.md').text
	def regexVersion = ((String) project.version).split("\\+")[0].replaceAll('\\.', /\\./).replaceAll('\\+', '\\+')
	def changelogRegex = ~"###? ${regexVersion}\\n\\n(( *- .+\\n)+)"
	def matcher = changelogText =~ changelogRegex

	if (matcher.find()) {
		def changelogContent = matcher.group(1)

		def changelogLines = changelogText.split('\n')
		def linkRefRegex = ~'^\\[([A-z0-9 _\\-/+.]+)]: '
		for (int i = changelogLines.length - 1; i > 0; i--) {
			def line = changelogLines[i]
			if ((line =~ linkRefRegex).find())
				changelogContent += '\n' + line
			else break
		}
		changelog = changelogContent
	} else {
		afterEvaluate {
			tasks.modrinth.setEnabled(false)
		}
	}
}
