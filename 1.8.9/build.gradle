plugins {
	id 'io.github.juuxel.loom-quiltflower' version '1.8.+'
	id "fabric-loom" version "1.2.1"
}

apply plugin: 'io.github.p03w.machete'

group = maven_group
version = "${project.version}+${project.minecraft_18}"
base.archivesName = "AxolotlClient"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	maven {
		name = "legacy-fabric"
		url = "https://repo.legacyfabric.net/repository/legacyfabric/"
	}
	mavenCentral()
}

loom {
	setIntermediaryUrl('https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar')
	accessWidenerPath.set(file("src/main/resources/axolotlclient.accesswidener"))
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_18}"
	mappings "net.legacyfabric:yarn:${project.mappings_18}"

	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"

	modImplementation("io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_18}")
	include "io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_18}"
	modImplementation "io.github.axolotlclient.AxolotlClient-config:AxolotlClientConfig-common:${project.config}"

	modImplementation "net.legacyfabric.legacy-fabric-api:legacy-fabric-api:${project.lfapi}+${project.minecraft_18}"

	modCompileOnlyApi(files("libs/legacy-modmenu-1.2.0+1.8.9-dev.jar"))

	implementation include(project(path: ":common", configuration: 'shadow'))

	modImplementation include("com.github.moehreag:search-in-resources:1.0.2")

	include("org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9") {
		exclude group: 'org.apache.logging.log4j', module: "log4j-api"
		exclude group: 'org.apache.logging.log4j', module: "log4j-core"
	}
	implementation include("org.slf4j:slf4j-api:1.7.36")
	localRuntime("org.slf4j:slf4j-jdk14:1.7.36")
}

configurations.configureEach {
	resolutionStrategy {
		dependencySubstitution {
			substitute module('io.netty:netty-all:4.0.23.Final') using module('io.netty:netty-all:4.0.26.Final')
		}
		force 'io.netty:netty-all:4.0.26.Final'
	}
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

java {
	withSourcesJar()
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/" + repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "p2rxzX0q"
	versionNumber = "${project.version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions.set(["${project.minecraft_18}"])
	loaders.set(["fabric", "quilt"])
	additionalFiles.set([remapSourcesJar])
	dependencies {
		required.project "legacy-fabric-api"
	}

	// Changelog fetching: Credit LambdAurora.
	// https://github.com/LambdAurora/LambDynamicLights/blob/1ef85f486084873b5d97b8a08df72f57859a3295/build.gradle#L145
	// License: MIT
	def changelogText = file('../CHANGELOG.md').text
	def regexVersion = ((String) project.version).split("\\+")[0].replaceAll('\\.', /\\./).replaceAll('\\+', '\\+')
	def changelogRegex = ~"###? ${regexVersion}\\n\\n(( *- .+\\n)+)"
	def matcher = changelogText =~ changelogRegex

	if (matcher.find()) {
		def changelogContent = matcher.group(1)

		def changelogLines = changelogText.split('\n')
		def linkRefRegex = ~'^\\[([A-z0-9 _\\-/+.]+)]: '
		for (int i = changelogLines.length - 1; i > 0; i--) {
			def line = changelogLines[i]
			if ((line =~ linkRefRegex).find())
				changelogContent += '\n' + line
			else break
		}
		changelog = changelogContent
	} else {
		afterEvaluate {
			tasks.modrinth.setEnabled(false)
		}
	}
}
