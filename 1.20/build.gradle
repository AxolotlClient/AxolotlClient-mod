plugins {
	id 'java'
	id "org.quiltmc.loom" version "1.1.+"
}

apply plugin: "io.github.p03w.machete"

group = maven_group
version = "${project.version}+${project.minecraft_120}"
archivesBaseName = "AxolotlClient"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	mavenLocal()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_120}"
	mappings "org.quiltmc:quilt-mappings:${project.mappings_120}:intermediary-v2"

	modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader}"

	modImplementation include("org.quiltmc.qsl.core:networking:${project.qsl_120}+${project.minecraft_120}")

	modImplementation("io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_120}") {
		exclude group: "com.terraformersmc"
	}
	include "io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_120}"
	modImplementation "io.github.axolotlclient.AxolotlClient-config:AxolotlClientConfig-common:${project.config}"

	modCompileOnlyApi("com.terraformersmc:modmenu:5.0.2")

	implementation include(project(path: ":common", configuration: 'shadow'))

	implementation('com.github.JnCrMx:discord-game-sdk4j:v0.5.5')
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

java {
	withSourcesJar()
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/" + repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "p2rxzX0q"
	versionNumber = "${project.version}"
	versionType = "release"
	uploadFile = remapJar
	gameVersions.set(["${project.minecraft_120}"])
	loaders.set(["quilt"])
	additionalFiles.set([remapSourcesJar])

	// Changelog fetching: Credit LambdAurora.
	// https://github.com/LambdAurora/LambDynamicLights/blob/1ef85f486084873b5d97b8a08df72f57859a3295/build.gradle#L145
	// License: MIT
	def changelogText = file('../CHANGELOG.md').text
	def regexVersion = ((String) project.version).split("\\+")[0].replaceAll('\\.', /\\./).replaceAll('\\+', '\\+')
	def changelogRegex = ~"###? ${regexVersion}\\n\\n(( *- .+\\n)+)"
	def matcher = changelogText =~ changelogRegex

	if (matcher.find()) {
		def changelogContent = matcher.group(1)

		def changelogLines = changelogText.split('\n')
		def linkRefRegex = ~'^\\[([A-z0-9 _\\-/+.]+)]: '
		for (int i = changelogLines.length - 1; i > 0; i--) {
			def line = changelogLines[i]
			if ((line =~ linkRefRegex).find())
				changelogContent += '\n' + line
			else break
		}
		changelog = changelogContent
	} else {
		afterEvaluate {
			tasks.modrinth.setEnabled(false)
		}
	}
}
