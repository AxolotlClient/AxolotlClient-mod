pluginManagement {
	repositories {
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Ornithe'
			url = 'https://maven.ornithemc.net/releases'
		}
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release/'
		}
		gradlePluginPortal()
	}
}

boolean checkProp(String property) {

	String value = getProperties().get("axolotlclient.modules." + property)
	return !(value == null || value.isEmpty() || value == "false" || value == "f")
}

void optional(String project) {
	if (checkProp(project) || checkProp("all")) {
		System.out.println("Adding " + project)
		include project
	}
}

include 'common'

System.out.println("Adding submodules..")

optional '1.8.9'
optional '1.19.3'
optional '1.19.2'
optional '1.16_combat-6'
optional '1.16.5'
optional '1.19.4'
optional '1.20'

