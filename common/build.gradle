plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '7.+'
}

group 'io.github.axolotlclient'
archivesBaseName = project.archives_base_name

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	maven {
		url "https://moehreag.duckdns.org/maven/snapshots"
	}
	maven {
		url "https://moehreag.duckdns.org/maven/releases"
	}

	maven {
		url 'https://repo.hypixel.net/repository/Hypixel/'
	}

	maven {
		name 'TerraformersMC Maven'
		url 'https://maven.terraformersmc.com/releases/'
	}

	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {

	// take the oldest version just to build against
	compileOnly("io.github.axolotlclient:AxolotlClient-config:${project.config}+${project.minecraft_18}")
	compileOnly("io.github.axolotlclient.AxolotlClient-config:AxolotlClientConfig-common:${project.config}")

	compileOnly("com.google.guava:guava:17.0")
	compileOnly("org.apache.httpcomponents:httpclient:4.3.3")
	compileOnly("com.google.code.gson:gson:2.2.4")
	compileOnly("commons-io:commons-io:2.4")
	compileOnly("org.apache.commons:commons-lang3:3.3.2")

	shadow(implementation("net.hypixel:hypixel-api-core:4.1") {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.apache.httpcomponents", module: "httpclient"
	})

	shadow(implementation("net.hypixel:hypixel-api-transport-apache:4.1") {
		exclude group: "com.google.code.gson", module: "gson"
		exclude group: "org.apache.httpcomponents", module: "httpclient"
	})

	shadow implementation('com.github.JnCrMx:discord-game-sdk4j:v0.5.5')
}

build {
	dependsOn(shadowJar)
	tasks.jar.enabled = false
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

tasks.withType(AbstractArchiveTask).configureEach {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

shadowJar {
	archiveClassifier.set('')
	minimize()
	relocate 'net.hypixel', 'axolotlclient.hypixel'
}

publishing {
	publications {
		shadow(MavenPublication) { publication ->
			project.shadow.component(publication)
		}
	}
	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/" + repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}

tasks.modrinth.enabled = false
